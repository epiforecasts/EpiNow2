functions {
#include functions/convolve.stan
#include functions/pmfs.stan
#include functions/gaussian_process.stan
#include functions/rt.stan
#include functions/infections.stan
#include functions/observation_model.stan
#include functions/generated_quantities.stan
}


data {
#include data/observations.stan
#include data/delays.stan
#include data/gaussian_process.stan
#include data/generation_time.stan
#include data/rt.stan
#include data/backcalc.stan
#include data/observation_model.stan
}

transformed data{
  // observations
  int ot = t - seeding_time - horizon;  // observed time
  int ot_h = ot + horizon;  // observed time + forecast horizon
  // gaussian process
  int noise_terms = setup_noise(ot_h, t, horizon, estimate_r, stationary, future_fixed, fixed_from);
  matrix[noise_terms, M] PHI = setup_gp(M, L, noise_terms);  // basis function
  // Rt
  real r_logmean = log(r_mean^2 / sqrt(r_sd^2 + r_mean^2));
  real r_logsd = sqrt(log(1 + (r_sd^2 / r_mean^2)));

  int gt_max_fixed =
    num_elements(gt_np_pmf) - num_elements(gt_np_pmf_groups) + 1;
  int gt_max_total = gt_max_fixed + sum(gt_max) - num_elements(gt_max);
  int delay_max_fixed =
    num_elements(delay_np_pmf) - num_elements(delay_np_pmf_groups) + 1;
  int delay_max_total = delay_max_fixed + sum(delay_max) - num_elements(delay_max);
  int trunc_max_fixed =
    num_elements(trunc_np_pmf) - num_elements(trunc_np_pmf_groups) + 1;
  int trunc_max_total = trunc_max_fixed + sum(trunc_max) - num_elements(trunc_max);

  vector[gt_max_fixed] gt_fixed_pmf;
  vector[trunc_max_fixed] trunc_fixed_pmf;
  vector[delay_max_fixed] delay_fixed_pmf;

  gt_fixed_pmf = convolve_ragged_pmf(
    gt_np_pmf, gt_np_pmf_groups, gt_max_fixed
  );

  trunc_fixed_pmf = convolve_ragged_pmf(
    trunc_np_pmf, trunc_np_pmf_groups, trunc_max_fixed
  );

  delay_fixed_pmf = convolve_ragged_pmf(
    delay_np_pmf, delay_np_pmf_groups, delay_max_fixed
  );
}

parameters{
  // gaussian process
  real<lower = ls_min,upper=ls_max> rho[fixed ? 0 : 1];  // length scale of noise GP
  real<lower = 0> alpha[fixed ? 0 : 1];    // scale of of noise GP
  vector[fixed ? 0 : M] eta;               // unconstrained noise
  // Rt
  vector[estimate_r] log_R;                // baseline reproduction number estimate (log)
  real initial_infections[estimate_r] ;    // seed infections
  real initial_growth[estimate_r && seeding_time > 1 ? 1 : 0]; // seed growth rate
  real<lower = 0> gt_mean[gt_n_p]; // parametric generation time means
  real<lower = 0> gt_sd[gt_n_p];     // parametric generation time sds
  real<lower = 0> bp_sd[bp_n > 0 ? 1 : 0]; // standard deviation of breakpoint effect
  real bp_effects[bp_n];                   // Rt breakpoint effects
  // observation model
  real<lower = 0> delay_mean[delay_n_p];         // mean of delays
  real<lower = 0> delay_sd[delay_n_p];  // sd of delays
  simplex[week_effect] day_of_week_simplex;// day of week reporting effect
  real<lower = 0, upper = 1> frac_obs[obs_scale];     // fraction of cases that are ultimately observed
  real trunc_mean[trunc_n_p];        // mean of truncation
  real<lower = 0> trunc_sd[trunc_n_p]; // sd of truncation
  real<lower = 0> rep_phi[model_type];     // overdispersion of the reporting process
}

transformed parameters {
  vector[fixed ? 0 : noise_terms] noise;                    // noise  generated by the gaussian process
  vector<lower = 0, upper = 10 * r_mean>[estimate_r > 0 ? ot_h : 0] R; // reproduction number
  vector[t] infections;                                     // latent infections
  vector[ot_h] reports;                                     // estimated reported cases
  vector[ot] obs_reports;                                   // observed estimated reported cases
  vector[gt_max_total] gt_rev_pmf;

  // GP in noise - spectral densities
  if (!fixed) {
    noise = update_gp(PHI, M, L, alpha[1], rho[1], eta, gp_type);
  }
  // Estimate latent infections
  if (estimate_r) {
    // via Rt
    gt_rev_pmf = combine_pmfs(
      gt_fixed_pmf, gt_mean, gt_sd, gt_max, gt_dist, gt_max_total, gt_zeroes, 1
    );
    R = update_Rt(
      ot_h, log_R[estimate_r], noise, breakpoints, bp_effects, stationary
    );
    infections = generate_infections(
      R, seeding_time, gt_rev_pmf, initial_infections, initial_growth, pop,
      future_time
    );
  } else {
    // via deconvolution
    infections = deconvolve_infections(
      shifted_cases, noise, fixed, backcalc_prior
    );
  }
  // convolve from latent infections to mean of observations
  {
    vector[delay_max_total] delay_rev_pmf;
    delay_rev_pmf = combine_pmfs(
      delay_fixed_pmf, delay_mean, delay_sd, delay_max, delay_dist, delay_max_total, 0, 1
    );
    reports = convolve_to_report(infections, delay_rev_pmf, seeding_time);
  }
 // weekly reporting effect
 if (week_effect > 1) {
   reports = day_of_week_effect(reports, day_of_week, day_of_week_simplex);
  }
  // scaling of reported cases by fraction observed
 if (obs_scale) {
   reports = scale_obs(reports, frac_obs[1]);
 }
 // truncate near time cases to observed reports
 {
   vector[trunc_max_total] trunc_rev_cmf;
   trunc_rev_cmf = reverse_mf(cumulative_sum(combine_pmfs(
     trunc_fixed_pmf, trunc_mean, trunc_sd, trunc_max, trunc_dist, trunc_max_total, 0, 0
   )));
   obs_reports = truncate(reports[1:ot], trunc_rev_cmf, 0);
 }
}

model {
  // priors for noise GP
  if (!fixed) {
    gaussian_process_lp(
      rho[1], alpha[1], eta, ls_meanlog, ls_sdlog, ls_min, ls_max, alpha_sd
    );
  }
  // penalised priors for delay distributions
  delays_lp(
    delay_mean, delay_mean_mean,
    delay_mean_sd, delay_sd, delay_sd_mean, delay_sd_sd,
    delay_dist, delay_weight
  );
  // priors for truncation
  delays_lp(
    trunc_mean, trunc_sd,
    trunc_mean_mean, trunc_mean_sd,
    trunc_sd_mean, trunc_sd_sd,
    trunc_dist, 1
  );
  if (estimate_r) {
    // priors on Rt
    rt_lp(
      log_R, initial_infections, initial_growth, bp_effects, bp_sd, bp_n,
      seeding_time, r_logmean, r_logsd, prior_infections, prior_growth
    );
    // penalised_prior on generation interval
    delays_lp(
      gt_mean, gt_mean_mean, gt_mean_sd, gt_sd, gt_sd_mean, gt_sd_sd, gt_dist,
      gt_weight
    );
  }
  // prior observation scaling
  if (obs_scale) {
    frac_obs[1] ~ normal(obs_scale_mean, obs_scale_sd) T[0, 1];
  }
  // observed reports from mean of reports (update likelihood)
  if (likelihood) {
    report_lp(
      cases, obs_reports, rep_phi, phi_mean, phi_sd, model_type, obs_weight
    );
  }
}

generated quantities {
  int imputed_reports[ot_h];
  vector[estimate_r > 0 ? 0: ot_h] gen_R;
  real r[ot_h];
  vector[return_likelihood ? ot : 0] log_lik;
  if (estimate_r){
    // estimate growth from estimated Rt
    real gen_gt_mean = pmf_mean(gt_rev_pmf, 1, 1);
    real gen_gt_var = pmf_var(gt_rev_pmf, 1, 1, gen_gt_mean);
    r = R_to_growth(R, gen_gt_mean, gen_gt_var);
  } else {
    // sample generation time
    real gt_mean_sample[gt_n_p];
    real gt_sd_sample[gt_n_p];
    vector[gt_max_total] gen_rev_pmf;
    real gen_gt_mean;
    real gen_gt_var;

    gt_mean_sample = normal_rng(gt_mean_mean, gt_mean_sd);
    gt_sd_sample = normal_rng(gt_sd_mean, gt_sd_sd);
    gen_rev_pmf = combine_pmfs(
      gt_fixed_pmf, gt_mean_sample, gt_sd_sample, gt_max, gt_dist, gt_max_total,
      gt_zeroes, 1
    );

    gen_gt_mean = pmf_mean(gt_rev_pmf, 1, 1);
    gen_gt_var = pmf_var(gt_rev_pmf, 1, 1, gen_gt_mean);
    // calculate Rt using infections and generation time
    gen_R = calculate_Rt(
      infections, seeding_time, gen_rev_pmf, rt_half_window
    );
    // estimate growth from calculated Rt
    r = R_to_growth(gen_R, gen_gt_mean, gen_gt_var);
  }
  // simulate reported cases
  imputed_reports = report_rng(reports, rep_phi, model_type);
  // log likelihood of model
  if (return_likelihood) {
    log_lik = report_log_lik(
      cases, obs_reports, rep_phi, model_type, obs_weight
    );
  }
}
